# cmake-format: off
# SPDX-FileCopyrightText: Copyright 2021-2025 NVIDIA Corporation
# SPDX-License-Identifier: Apache-2.0
#
# Modifications Copyright (C) 2024-2026 Advanced Micro Devices, Inc. All rights reserved.
#
# cmake-format: on

if (NOT TARGET deps::libcuckoo)
    # CMP0169 OLD needed: FetchContent_Populate() with declared details is used
    # here to apply a patch after fetch, which requires the legacy behavior.
    cmake_policy(PUSH)
    cmake_policy(SET CMP0169 OLD)

    FetchContent_Declare(
            deps-libcuckoo
            GIT_REPOSITORY https://github.com/efficient/libcuckoo
            GIT_TAG master
            GIT_SHALLOW TRUE
    )
    FetchContent_GetProperties(deps-libcuckoo)
    message(STATUS "Patch directory: ${deps-libcuckoo_SOURCE_DIR}")
    if (NOT deps-libcuckoo_POPULATED)
        message(STATUS "Fetching libcuckoo sources")
        FetchContent_Populate(deps-libcuckoo)
        message(STATUS "Fetching libcuckoo sources - done")

        message(STATUS "Applying patch for libcuckoo")
        find_package(Git)
            execute_process(
                COMMAND bash -c "${GIT_EXECUTABLE} reset HEAD --hard && ${GIT_EXECUTABLE} apply --verbose ${CMAKE_CURRENT_LIST_DIR}/libcuckoo.patch"
                WORKING_DIRECTORY "${deps-libcuckoo_SOURCE_DIR}"
                RESULT_VARIABLE exec_result
                ERROR_VARIABLE exec_error
                ERROR_STRIP_TRAILING_WHITESPACE
                OUTPUT_VARIABLE exec_output
                OUTPUT_STRIP_TRAILING_WHITESPACE
                )
            if(exec_result EQUAL 0)
                message(STATUS "Applying patch for libcuckoo - done")
            else()
                message(STATUS "Applying patch for libcuckoo - failed")
                message(FATAL_ERROR "${exec_output}\n${exec_error}")
            endif()
    endif ()

    # Create static library
    cucim_set_build_shared_libs(OFF)
    add_subdirectory(${deps-libcuckoo_SOURCE_DIR} ${deps-libcuckoo_BINARY_DIR} EXCLUDE_FROM_ALL)
    # libcuckoo's CMakeLists.txt is not compatible with `add_subdirectory` method (it uses ${CMAKE_SOURCE_DIR} instead of ${CMAKE_CURRENT_SOURCE_DIR})
    # so add include directories explicitly.
    target_include_directories(libcuckoo INTERFACE
        $<BUILD_INTERFACE:${deps-libcuckoo_SOURCE_DIR}>
    )

    # This prevents 'unused parameter' and 'reorder-ctor' warnings from being treated as errors
    target_compile_options(libcuckoo INTERFACE
            -Wno-unused-parameter
            -Wno-reorder-ctor
    )

    cucim_restore_build_shared_libs()

    add_library(deps::libcuckoo INTERFACE IMPORTED GLOBAL)
    target_link_libraries(deps::libcuckoo INTERFACE libcuckoo)
    set(deps-libcuckoo_SOURCE_DIR ${deps-libcuckoo_SOURCE_DIR} CACHE INTERNAL "" FORCE)
    mark_as_advanced(deps-libcuckoo_SOURCE_DIR)

    cmake_policy(POP)
endif ()
